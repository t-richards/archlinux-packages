# Maintainer: Tom Richards <tom@tomrichards.net>
#
# Remarks:
#
# This is Tom's custom build of Ruby 3.4.7. It is based on the Arch Linux
# PKGBUILD for Ruby, with the following changes:
#
# - No split subpackages such as ruby-docs, ruby-ri, ruby-test, etc.
# - Includes bundler
# - Uses jemalloc instead of glibc malloc
# - Enables YJIT
#
pkgname=(ruby-custom)
pkgver=3.4.7
pkgrel=1
arch=(x86_64)
url='https://www.ruby-lang.org/en/'
license=(BSD-2-Clause)
pkgdesc='An object-oriented language for quick and easy programming'
depends=(gdbm jemalloc openssl libffi libyaml libxcrypt gmp zlib)
makedepends=(doxygen gdbm graphviz jemalloc libffi libyaml openssl rust ttf-dejavu tk)
optdepends=(
  'tk: for Ruby/TK'
)
provides=(ruby ruby-bundler)
conflicts=(ruby ruby-bundler)
options=(!emptydirs)
_osslver=3.3.2
source=(https://cache.ruby-lang.org/pub/ruby/${pkgver:0:3}/ruby-${pkgver}.tar.xz
        ruby-openssl-$_osslver.tar.xz::https://github.com/ruby/openssl/archive/refs/tags/v$_osslver.tar.gz)
sha512sums=('a6b99a2f1d0115d5e7efa710da440b9066c524c335928367c80852630f8db5da36c0a82d6e7ace90e8c40cb20c6097cbdca15a51c343254cadf5f0adf60f8505'
            'a774a8653ce81c457da12eb4f72ea9d138eade457dcda2543c3d042bce1c9fed2879231bdad05e7d40e73dfba0e0587a8bdf863ad6c83a523ed6e7b8ee59447c')

prepare() {
  cd ruby-${pkgver}
  rm -rf ext/openssl test/openssl
  mv ../openssl-$_osslver/ext/openssl ext
  mv ../openssl-$_osslver/lib ext/openssl
  mv ../openssl-$_osslver/{History.md,openssl.gemspec} ext/openssl
  mv ../openssl-$_osslver/test/openssl test
}

build() {
  cd ruby-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sharedstatedir=/var/lib \
    --libexecdir=/usr/lib/ruby \
    --enable-shared \
    --disable-rpath \
    --with-dbm-type=gdbm_compat \
    --with-jemalloc \
    --enable-yjit

  make
}

check() {
  cd ruby-${pkgver}

  make test
}

package() {
  cd ruby-${pkgver}

  make DESTDIR="${pkgdir}" install
}
