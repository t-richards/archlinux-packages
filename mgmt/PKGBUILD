# Maintainer: Tom Richards <tom@tomrichards.net>

pkgname=mgmt
pkgver=0.0.26
pkgrel=3
pkgdesc='Next generation config management.'
arch=('x86_64' 'armv6h' 'armv7h')
url="https://github.com/purpleidea/mgmt"
license=(GPL-3.0-only)
makedepends=('go' 'git' 'go-tools' 'ragel')
depends=('augeas' 'libvirt')
options=(!debug)
backup=("etc/${pkgname}/${pkgname}.conf")

source=("${pkgname}::git+${url}.git")
sha256sums=('SKIP')

build() {
    cd "${srcdir}/${pkgname}"

    export GOPATH="${srcdir}"
    export PATH="${PATH}:${GOPATH}/bin"
    go install github.com/blynn/nex@latest
    go mod tidy

    make VERSION="${pkgver}" SVERSION="${pkgver}" PROGRAM="${pkgname}"
}

package() {
    cd "${srcdir}/${pkgname}"

    install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
    install -Dm644 "misc/bashrc.sh" "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
    install -Dm644 "misc/example.conf" "${pkgdir}/etc/${pkgname}/${pkgname}.conf"
}
